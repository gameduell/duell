/*
 * Copyright (c) 2003-2015, GameDuell GmbH
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * 1. Redistributions of source code must retain the above copyright notice,
 * this list of conditions and the following disclaimer.
 *
 * 2. Redistributions in binary form must reproduce the above copyright notice,
 * this list of conditions and the following disclaimer in the documentation
 * and/or other materials provided with the distribution.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
 * ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
 * WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 * IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY
 * DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
 * (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
 * LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
 * ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

package duell.helpers;

import duell.objects.SemVer;
import duell.helpers.PlatformHelper;

import sys.io.Process;

enum Platform
{
	ANDROID;
	BLACKBERRY;
	FIREFOXOS;
	FLASH;
	HTML5;
	IOS;
	LINUX;
	MAC;
	TIZEN;
	WINDOWS;
	WEBOS;
	EMSCRIPTEN;
	MACANE;
}

enum Architecture
{
	ARMV5;
	ARMV6;
	ARMV7;
	X86;
	X64;
}

class PlatformHelper
{
	public static var hostArchitecture(get_hostArchitecture, null) : Architecture;
	public static var hostPlatform(get_hostPlatform, null) : Platform;

    /** Check will only work when the platform is Mac OS X */
    public static var macVersion(get_macVersion, null) : SemVer;

	private static var _hostArchitecture : Architecture;
	private static var _hostPlatform : Platform;
    private static var _macVersion : SemVer;

	private static function get_hostArchitecture() : Architecture
	{
		if (_hostArchitecture == null)
		{
			switch (hostPlatform)
			{
				case WINDOWS:

					var architecture = Sys.getEnv("PROCESSOR_ARCHITEW6432");
					if(architecture != null && architecture.indexOf("64") > -1)
					{
						_hostArchitecture = Architecture.X64;
					}
					else
					{
						_hostArchitecture = Architecture.X86;
					}

				case LINUX, MAC:

					var process = new Process("uname", [ "-m" ]);
					var output = process.stdout.readAll().toString();
					var error = process.stderr.readAll().toString();
					process.exitCode();
					//process.close();

					if (output.indexOf ("64") > -1)
					{
						_hostArchitecture = Architecture.X64;

					}
					else
					{
						_hostArchitecture = Architecture.X86;
					}

				default:

					_hostArchitecture = Architecture.ARMV6;

			}

			LogHelper.info("", " - \x1b[1mDetected host architecture:\x1b[0m " + StringHelper.formatEnum(_hostArchitecture));
		}

		return _hostArchitecture;
	}


	private static function get_hostPlatform() : Platform
	{
		if (_hostPlatform == null)
		{
			var systemName = Sys.systemName();

			if (new EReg("window", "i").match(systemName))
			{
				_hostPlatform = Platform.WINDOWS;
			}
			else if (new EReg("linux", "i").match(systemName))
			{
				_hostPlatform = Platform.LINUX;
			}
			else if (new EReg("mac", "i").match(systemName))
			{
                _hostPlatform = Platform.MAC;
			}

			var platformName : String = StringHelper.formatEnum(_hostPlatform);
			LogHelper.info("", " - \x1b[1mDetected host platform:\x1b[0m " + platformName);
		}

		return _hostPlatform;
	}

    private static function get_macVersion() : SemVer
    {
        if (hostPlatform != Platform.MAC)
        {
            LogHelper.exitWithFormattedError("PlatformHelper.macVersion called and the host platform is not MAC");
            return null;
        }

        if (_macVersion == null)
        {
            var process = new Process("sw_vers", [ "-productVersion" ]);
            var output = process.stdout.readAll().toString();
            process.exitCode();

            var versions: Array<Int> = output.split(".").map(function(s) { return Std.parseInt(s); });
            _macVersion = new SemVer(versions[0], versions[1], versions[2], false, false);
        }

        return _macVersion;
    }
}
